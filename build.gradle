plugins {
    id 'java'
    id 'idea'
    id 'eclipse'
    id 'maven'

    id 'org.ajoberstar.grgit' version '4.0.1'
    id 'com.github.johnrengelman.shadow' version '5.2.0'
}

group 'wecross.stub.chainmaker'
version '1.0.0'

sourceCompatibility = 1.8
targetCompatibility = 1.8

// In this section you declare where to find the dependencies of your project
repositories {
    // Use jcenter for resolving your dependencies.
    // You can declare any Maven/Ivy/file repository here.
    jcenter()
    maven { url "https://maven.aliyun.com/nexus/content/groups/public/" }
    maven { url "https://oss.sonatype.org/content/repositories/snapshots" }
    mavenCentral()
}

configurations {
    all*.exclude group: 'org.ethereum', module: 'solcJ-all'
    all*.exclude group: 'ch.qos.logback', module: 'logback-classic'
    all*.exclude group: 'org.apache.logging.log4j', module: 'log4j-slf4j-impl'
}

def log4j_version="2.19.0"
List logger = [
        "org.apache.logging.log4j:log4j-api:$log4j_version",
        "org.apache.logging.log4j:log4j-core:$log4j_version",
        "org.apache.logging.log4j:log4j-slf4j-impl:$log4j_version",
        "org.apache.logging.log4j:log4j-web:$log4j_version",
        "org.apache.logging.log4j:log4j-jul:$log4j_version"
]

dependencies {
    compile(logger)
    compile (group: 'org.yaml', name: 'snakeyaml', version: '1.29')
    // stub接口定义
    implementation (group: 'com.webank', name: 'wecross-java-stub', version: '1.3.0') {
        exclude group: 'com.google.code.gson', module: 'gson'
        exclude group: "io.netty"
        exclude group: "org.fisco-bcos", module: "tcnative"
    }

    // chainmaker-sdk-java
    implementation (group: 'org.chainmaker', name: 'chainmaker-sdk-java', version: '2.3.1.3')
    // chainmaker-sdk-java 中需要的依赖
    implementation files("./lib/netty-tcnative-openssl-static-2.0.39.Final.jar")
    //compile files("./lib/netty-handler-cm-4.1.53.Final.jar")
    implementation (group: 'org.apache.commons', name: 'commons-pool2', version: '2.11.1')
    implementation (group: 'commons-collections', name: 'commons-collections', version: '3.2.2')
    implementation (group: 'org.bouncycastle', name: 'bcpkix-jdk15on', version: '1.62')
    implementation (group: 'commons-io', name: 'commons-io', version: '2.6')
    implementation (group: 'com.alibaba', name: 'fastjson', version: '1.2.62')
    implementation (group: 'commons-logging', name: 'commons-logging', version: '1.2')
    implementation(group: 'com.google.protobuf', name: 'protobuf-java-util', version: '3.2.0') {
        exclude group: 'com.google.guava', module: 'guava'
        exclude group: 'com.google.protobuf', module: 'protobuf-java'
    }
    compile (group: 'io.netty', name: 'netty-handler', version: '4.1.53.Final')
    //implementation (group: 'io.grpc', name: 'grpc-core', version: '1.23.0')
    implementation (group: 'io.grpc', name: 'grpc-api', version: '1.23.0')
    implementation (group: 'io.grpc', name: 'grpc-netty', version: '1.23.0') {
        exclude group: 'io.netty',  module: 'netty-handler'
    }
    implementation (group: 'io.grpc', name: 'grpc-stub', version: '1.23.0')
    implementation (group: 'io.grpc', name: 'grpc-protobuf', version: '1.23.0')
    implementation (group: 'org.web3j', name: 'abi', version: '5.0.0')
    implementation (group: 'net.java.dev.jna', name: 'jna', version: '5.6.0')

    // Use JUnit test framework
    testImplementation 'junit:junit:4.13.1'
    testImplementation 'org.slf4j:slf4j-log4j12:1.7.36'
    testImplementation 'org.springframework.boot:spring-boot-starter-test:2.6.7'
}

// clean dist、build、log directory
task cleanAll(type: Delete) {
    delete 'dist'
    delete 'build'
    delete 'log'
    delete 'out'
}

tasks.clean.dependsOn(tasks.cleanAll)

shadowJar {
    mergeServiceFiles()
    minimize()
}

// 打包操作，打包项目所有依赖生成 fat jar
task makeJar(type: com.github.jengelman.gradle.plugins.shadow.tasks.ShadowJar, dependsOn: shadowJar) {

    destinationDir file('dist/apps')
    archiveName project.name + "-" + project.version + '.jar'
    exclude '**/*.xml'
    exclude '**/*.toml'
    exclude '**/*.properties'
    exclude '**/*.yml'

    from zipTree(shadowJar.getArchiveFile())
}

jar.enabled = false
project.tasks.assemble.dependsOn(makeJar)